
# https://github.com/ellzey/dotfiles/blob/master/gdbinit
define dumptailq
	set $tailq_head = $arg0
	set $ent        = (($tailq_head)->tqh_first)

	while ($ent != (void *)0l)
		print *$ent
		set $ent = ($ent)->next.tqe_next
	end
end
document dumptailq
	Print a sys/queue.h TAILQ structure, assumes TAILQ_ENTRY is next
	Usage: dumptailq tailq head
end

define tbt
	thread apply all bt
end
document tbt
	Print backtrace of all running threads
end

define malloc_info
	set $malloc_break_loop = 0
	set $start = &main_arena
	set $malloc_system_mem = 0
	set $malloc_max_system_mem = 0

	while ($start != 0 && $malloc_break_loop != 1)
		set $malloc_system_mem += $start->system_mem
		set $malloc_max_system_mem += $start->max_system_mem

		if ($start == $start->next)
			set $malloc_break_loop = 1
		end
		set $start = $start->next
	end

	printf "malloc_system_mem: %lu\n", $malloc_system_mem
	printf "malloc_max_system_mem: %lu\n", $malloc_max_system_mem
end

define container_of
    set $_container_of_offset = &((struct $arg1 *)0)->$arg2
    print ((struct $arg1 *)((char *)$arg0 - (char *)$_container_of_offset))
end
document container_of
    Like that one from linux kernel headers
    Usage: container_of ptr name_of_struct member
    Example: container_of inode.i_dentry.first dentry d_name

    (gdb) container_of dir_dentry->d_inode.i_dentry.first dentry d_alias
    $50 = (struct dentry *) 0xffff88007b9f1640
    (gdb) p $50->d_name
    $51 = {
      {
        {
          hash = 7303014,
          len = 3
        },
        hash_len = 12892204902
      },
      name = 0xffff88007b9f16b0 "foo"
    }
end



